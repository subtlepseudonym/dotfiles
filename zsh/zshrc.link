# default environment
export DOTFILES="${HOME}/.dotfiles"

export HISTFILE="${DOTFILES}/zsh/zsh_history"
export HISTSIZE=2000
export SAVEHIST=10000

# add user binaries and scripts to path
workspace="${HOME}/workspace"
export path=(
	"${workspace}/bin"
	"${workspace}/script"
	"${path[@]}"
)
export fpath=(
	"${DOTFILES}/zsh/completion"
	"${DOTFILES}/zsh/function"
	"${fpath[@]}"
)

# default completions
zstyle ':completion:*' completer _complete _ignored _approximate
zstyle ':completion:*' matcher-list '' 'm:{[:lower:][:upper:]}={[:upper:][:lower:]}'
zstyle ':completion:*' squeeze-slashes true
zstyle :compinstall filename "${HOME}/.zshrc"
autoload -Uz compinit && compinit

# load config files
for file in $DOTFILES/**/(*).zsh
do
	source "${file}"
done

# local environment variables / secrets
if [[ -a "${HOME}/.localrc" ]]; then
	source "${HOME}/.localrc"
fi

# bind zsh-history-substring-search functions to arrow keys
if [[ "$terminfo" ]]; then
	bindkey "${terminfo[kcuu1]}" history-substring-search-up
	bindkey "${terminfo[kcud1]}" history-substring-search-down
fi
bindkey '^[[A' history-substring-search-up
bindkey '^[[B' history-substring-search-down
unset HISTORY_SUBSTRING_SEARCH_HIGHLIGHT_FOUND
unset HISTORY_SUBSTRING_SEARCH_HIGHLIGHT_NOT_FOUND

# prevent duplicates in history
setopt HIST_IGNORE_DUPS
setopt HIST_FIND_NO_DUPS

# use custom word delimiters
WORDCHARS='~!#$%^&*()_-=[]{}<>?'

# fix terminal colors for raspis
if [[ "${TERM}" == "xterm" ]]; then
	export TERM="xterm-256color"
fi

# attempt to apply zsh theme
export THEME="${THEME:-loki}"
theme_file="${DOTFILES}/themes/${THEME}.zsh-theme"
[ -f "${theme_file}" ] && source "${DOTFILES}/themes/${THEME}.zsh-theme"
